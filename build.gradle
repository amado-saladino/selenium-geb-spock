plugins {
    id "groovy"
    id 'ru.vyarus.quality' version '2.2.0'
    id "io.qameta.allure" version "2.5"
}

configurations {
    agent
}

ext {
    drivers = ["firefox", "chrome", "chromeHeadless"]
}

repositories {
    mavenCentral()
}

dependencies {
    agent "org.aspectj:aspectjweaver:1.8.10"
    compile 'ru.yandex.qatools.ashot:ashot:1.5.4'
    compile 'io.qameta.allure:allure-testng:2.7.0'
    compile group: 'io.codearte.jfairy', name: 'jfairy', version: '0.5.9'
    compile group: 'com.opencsv', name: 'opencsv', version: '4.5'
    compile group: 'org.json', name: 'json', version: '20180813'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    testCompile "org.gebish:geb-spock:3.0"
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    compile group: 'org.gebish', name: 'geb-testng', version: '3.0'
    testCompile "org.seleniumhq.selenium:selenium-java:3.14.0"
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.1'
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false }  // Always run tests

        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}

allure {
    autoconfigure = true
    version = '2.5.0'
}